ext {
    javaVersion = JavaVersion.VERSION_1_8
    springBootVersion = "2.1.11.RELEASE"
    springCloudVersion = "Greenwich.SR4"
    springCloudAlibabaVersion = "2.1.1.RELEASE"
    micaAutoVersion = "1.2.0"
    apolloVersion = "1.4.0"
    protostuffVersion = "1.6.0"
    disruptorVersion = "3.4.2"
    swaggerVersion = "2.9.2"
    swaggerBootstrapUiVersion = "1.9.6"
    okhttpVersion = "3.14.4"
    jsoupVersion = "1.12.1"
    redissonVersion = "3.11.6"
    mybatisPlusVersion = "3.2.0"
    lombokVersion = "1.18.10"
    findbugsVersion = "3.0.2"
}

configure(subprojects) {
    group = GROUPID
    version = VERSION
    apply plugin: 'signing'
    apply plugin: 'maven-publish'
}

configure(subprojects - project(":mica-bom")) {
    apply plugin: "java-library"
    apply from: "${rootProject.projectDir}/gradle/publish-jar.gradle"
    apply from: "${rootProject.projectDir}/gradle/quality.gradle"
    sourceCompatibility = "$javaVersion"
    targetCompatibility = "$javaVersion"
    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }

    repositories {
        mavenLocal()
        maven { url "https://maven.aliyun.com/repository/public" }
        maven { url "https://maven.aliyun.com/repository/spring" }
        maven { url "https://maven.aliyun.com/repository/spring-plugin" }
        maven { url "https://repo.spring.io/libs-release" }
        maven { url "https://repo.spring.io/milestone" }
        mavenCentral()
    }

    dependencies {
        // import BOM.
        implementation platform("org.springframework.boot:spring-boot-dependencies:$springBootVersion")
        implementation platform("org.springframework.cloud:spring-cloud-dependencies:$springCloudVersion")
        implementation platform("com.alibaba.cloud:spring-cloud-alibaba-dependencies:$springCloudAlibabaVersion")
        compileOnly "org.slf4j:slf4j-api"
        compileOnly "javax.servlet:javax.servlet-api"
        compileOnly "org.springframework:spring-web"
        compileOnly "org.projectlombok:lombok:$lombokVersion"
        annotationProcessor "org.projectlombok:lombok:$lombokVersion"
        testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"
        // swagger 暂时有问题 https://github.com/springfox/springfox/issues/2763
//        annotationProcessor "org.springframework:spring-context-indexer:${springVersion}"
        // JSR-305 only used for non-required meta-annotations
        compileOnly "com.google.code.findbugs:jsr305:$findbugsVersion"
        testCompileOnly "com.google.code.findbugs:jsr305:$findbugsVersion"
        testCompileOnly "org.projectlombok:lombok:$lombokVersion"
        testCompileOnly "org.springframework:spring-test"
        testImplementation "junit:junit"
    }

    compileJava.dependsOn(processResources)
}

def getRepositoryUsername() {
    return System.getenv('NEXUS_OSS_USER_NAME') ?: NEXUS_OSS_USER_NAME
}

def getRepositoryPassword() {
    return System.getenv('NEXUS_OSS_PASS_WORD') ?: NEXUS_OSS_PASS_WORD
}
